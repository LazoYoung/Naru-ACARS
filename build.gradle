plugins {
	id 'java'
	id 'edu.sc.seis.launch4j' version '2.5.4'
	id 'org.springframework.boot' version '3.0.2'
	id 'io.spring.dependency-management' version '1.1.0'
}

group = 'com.flylazo.naru_acars'
version = '1.1.1'
sourceCompatibility = '17'
targetCompatibility = '17'
var fsuipc = 'lib/FSUIPC-1.0.2.jar'
var xpc = 'lib/XPlaneConnect.jar'

repositories {
	mavenCentral()
}

dependencies {
	// Spring Boot
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	// Java Chromium Embedded Framework
	implementation 'me.friwi:jcefmaven:110.0.25'

	// Burningwave Core (Let us export module packages required for JCEF)
	implementation 'org.burningwave:core:12.62.5'

	// Simulator bridges
	implementation files(fsuipc)
	implementation files(xpc)

	// Mockito
	testImplementation "org.mockito:mockito-core:3.+"

	// OpenCSV
	//noinspection VulnerableLibrariesLocal
	implementation 'com.opencsv:opencsv:5.7.1'

	// Jakarta XML Binding API
	implementation 'org.eclipse.persistence:org.eclipse.persistence.moxy:4.0.0'
	implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'
	implementation 'jakarta.activation:jakarta.activation-api:2.1.1'

	// Jackson Javascript Mapper API
	implementation 'com.fasterxml.jackson.core:jackson-core:2.14.2'
}

launch4j {
	mainClassName = 'org.springframework.boot.loader.JarLauncher'
	productName = rootProject.name
	dontWrapJar = false
	headerType = 'gui'
	errTitle = 'Java not installed!'
	windowTitle = 'Naru ACARS'
	mutexName = 'naruacars'
	downloadUrl = 'https://www.oracle.com/java/technologies/downloads/'
	stayAlive = false
	jarTask = project.tasks.bootJar
}

tasks.named('test') {
	useJUnitPlatform()
}

processResources {
	filesMatching(['**/*.properties']) {
		filter {
			it.replace('%VERSION%', version)
		}
	}
}